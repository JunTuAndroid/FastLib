apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        vectorDrawables.useSupportLibrary true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //以下为配置library注释在打包jar后保留
    // 打包源码jar
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }
    // 打包文档jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}
dependencies {
    compileOnly rootProject.ext.dependencies["design"]
    compileOnly rootProject.ext.dependencies["appcompat"]
    compileOnly rootProject.ext.dependencies["recyclerview"]
    //万能适配器
    compileOnly rootProject.ext.dependencies["BaseRecyclerViewAdapterHelper"]
    //webView库
    compileOnly rootProject.ext.dependencies["agentweb"]
    //下拉刷新库
    compileOnly rootProject.ext.dependencies["smartRefreshLayout"]
    //图片加载
    api rootProject.ext.dependencies["glide"]
    //常用UI控件(TitleBarView、RadiusView等)
    api rootProject.ext.dependencies["UIWidget-Core"]
    //日志打印
    api rootProject.ext.dependencies["logger"]
    //注解
    api (rootProject.ext.dependencies["butterknife"]){
        exclude module :'support-annotations'
        exclude module :'support-compat'
    }
    //retrofit+rxjava 网络请求及数据解析相关
    api rootProject.ext.dependencies["rxJava"]
    api (rootProject.ext.dependencies["rxAndroid"]){
        exclude module :'rxjava'
    }
    api rootProject.ext.dependencies["retrofit"]

    api (rootProject.ext.dependencies["converter-gson"]){
        exclude module :'gson'
        exclude module :'retrofit'
    }
    api (rootProject.ext.dependencies["adapter-rxjava2"]){
        exclude module :'rxjava'
        exclude module :'retrofit'
    }
    api (rootProject.ext.dependencies["logging-interceptor"]){
        exclude module : 'okhttp'
    }
    api rootProject.ext.dependencies["gson"]
    //处理rxjava内存泄漏-生命周期绑定
    api (rootProject.ext.dependencies["rxlifecycle"]){
        exclude module : 'rxjava'
        exclude module : 'support-annotations'
        exclude module : 'support-compat'
        exclude module : 'support-core-utils'
        exclude module : 'support-core-ui'
        exclude module : 'support-fragment'
    }
    //滑动返回Activity
    api rootProject.ext.dependencies["swipebacklayout"]
    //快速Tab库
    api rootProject.ext.dependencies["flycoTabLayout"]
    //页面事件交互
    api rootProject.ext.dependencies["androideventbus"]
    //多状态视图切换
    api rootProject.ext.dependencies["EasyStatusView"]
}
